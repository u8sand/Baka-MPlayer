# Maintainer: u8sand <u8sand@gmail.com>
# Contributor: Alfredo Ramos <alfredo dot ramos at yandex dot com>

_pkgname=baka-mplayer
pkgname=${_pkgname}-git
pkgver=2.0.2.r188.g0b6ee2e
pkgrel=2
pkgdesc='A free and open source, cross-platform, libmpv based multimedia player. Qt5 build. Development version.'
arch=('i686' 'x86_64')
url='http://bakamplayer.u8sand.net/'
license=('GPL2')

depends=('mpv' 'qt5-x11extras' 'qt5-svg' 'xdg-utils')
optdepends=(
	'mpv-git: for bleeding-edge mpv features'
	'qt5-translations: for Qt5 dialog translations'
	'youtube-dl: for remote video streaming'
)
makedepends=('git' 'qt5-tools' 'qt5-declarative')
provides=("${_pkgname}=${pkgver}")
conflicts=("${_pkgname}")

install=${pkgname}.install

source=(
	"${_pkgname}::git+https://github.com/u8sand/Baka-MPlayer.git"
	"${pkgname}.install"
)
sha512sums=(
	'SKIP'
	'd590345e9b87e4350b7420eebf8f69e4ad65a53415257573b569ed1c85568a40b4f65fc8df5925f7c03af75d340c52a10a4d1389e5d868816aef9711102ebef1'
)

pkgver() {
	# Updating package version
	cd ${srcdir}/${_pkgname}
	(
		set -o pipefail
		git describe --long --tags 2>/dev/null | sed -r 's/^v//;s/([^-]*-g)/r\1/;s/-/./g' ||
		printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
	)
}

prepare() {
	# Make build directory
	mkdir ${srcdir}/build
}

build() {	
	# Building package
	cd ${srcdir}/build
	qmake-qt5 ../${_pkgname}/src \
		CONFIG+=release \
		CONFIG+=install_translations \
		-spec linux-g++
	make -j $(grep -c ^processor /proc/cpuinfo)
}

package() {
	# Installing package
	cd ${srcdir}/build
	make INSTALL_ROOT=${pkgdir} install
}